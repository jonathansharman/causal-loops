import Graphics2D;
import TwoCansAlpha4;

// A tile in a level.
class Tile {
	// The level this object is in.
	field level;
	// Row-column position.
	field position;

	constructor(level, position) {
		this.level = level;
		this.position = position;
	}

	function isObstacle() { return false; }
	function isSlippery() { return false; }
}

class Floor : Tile {
	constructor(level, position) : base(level, position) {}

	function draw() {
		xy = this.level.rowColToXY(this.position[0], this.position[1]);
		Images.get("images/floor.png").draw(xy[0], xy[1]);
	}

	function receiveFrom(object, direction) {
		return object.enterFloor(this, direction);
	}
}

class Wall : Tile {
	constructor(level, position) : base(level, position) {}

	function draw() {
		xy = this.level.rowColToXY(this.position[0], this.position[1]);
		Images.get("images/wall.png").draw(xy[0], xy[1]);
	}

	function isObstacle() { return true; }

	function receiveFrom(object, direction) {
		return object.enterWall(this, direction);
	}
}

class Ice : Tile {
	constructor(level, position) : base(level, position) {}

	function draw() {
		xy = this.level.rowColToXY(this.position[0], this.position[1]);
		Images.get("images/ice.png").draw(xy[0], xy[1]);
	}

	function isSlippery() { return true; }

	function receiveFrom(object, direction) {
		return object.enterIce(this, direction);
	}
}

class Hole : Tile {
	constructor(level, position) : base(level, position) {}

	function draw() {
		xy = this.level.rowColToXY(this.position[0], this.position[1]);
		Images.get("images/hole.png").draw(xy[0], xy[1]);
	}

	function receiveFrom(object, direction) {
		return object.enterHole(this, direction);
	}
}

class Stairs : Tile {
	constructor(level, position) : base(level, position) {}

	function draw() {
		xy = this.level.rowColToXY(this.position[0], this.position[1]);
		Images.get("images/stairs.png").draw(xy[0], xy[1]);
	}

	function receiveFrom(object, direction) {
		return object.enterStairs(this, direction);
	}
}

class SunkenCrate : Floor {
	constructor(level, position) : base(level, position) {}

	function draw() {
		xy = this.level.rowColToXY(this.position[0], this.position[1]);
		Images.get("images/sunken-crate.png").draw(xy[0], xy[1]);
	}
}

class TrappedCharacter : Floor {
	constructor(level, position) : base(level, position) {}

	function draw() {
		xy = this.level.rowColToXY(this.position[0], this.position[1]);
		Images.get("images/trapped-character.png").draw(xy[0], xy[1]);
	}
}
