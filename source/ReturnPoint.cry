import Graphics2D;
import Math;
import TwoCansAlpha4;

const RETURN_POINT_TILE_OFFSET = 0.5;

// A point from which a character emerged from the future.
class ReturnPoint {
	// The level this object is in.
	field level;
	// Row-column position.
	field rowCol;
	// Time until the return should occur.
	field time;
	// The character that needs to return to this point.
	field character;
	// Whether the return has occurred.
	field satisfied = false;

	constructor(level, rowCol, time, character) {
		this.level = level;
		this.rowCol = rowCol;
		this.time = time;
		this.character = character;
	}

	function update() {
		--this.time;
		if (this.time == 0) {
			if (this.character.inLevel && this.character.rowCol[0] == this.rowCol[0] && this.character.rowCol[1] == this.rowCol[1]) {
				this.character.removeFromLevel();
				--this.level.characterCount;
				this.satisfied = true;
			} else {
				// Logical inconsistency!
				this.level.consistent = false;
			}
		}
	}

	function draw() {
		characterTopLeft = this.level.rowColToXY(this.character.rowCol);
		// Draw triangle from character to return point, if not at same position.
		x = characterTopLeft[0] + this.level.tileSize / 2.0;
		y = characterTopLeft[1] + this.level.tileSize / 2.0;
		if (this.character.rowCol[0] != this.rowCol[0] || this.character.rowCol[1] != this.rowCol[1]) {
			theta = Math.arctan
				( this.character.rowCol[0] - this.rowCol[0]
				, this.character.rowCol[1] - this.rowCol[1]
				);
			theta1 = theta + Math.PI / 2.0;
			theta2 = theta - Math.PI / 2.0;
			offset = this.level.tileSize * RETURN_POINT_TILE_OFFSET;
			p1 = [x + Math.cos(theta1) * offset, y + Math.sin(theta1) * offset];
			p2 = [x + Math.cos(theta2) * offset, y + Math.sin(theta2) * offset];
			//xy = this.level.rowColToXY(this.rowCol);
			//p3 = [xy[0] + this.level.tileSize / 2.0, xy[1] + this.level.tileSize / 2.0];
			p3 = this.level.rowColToXY([this.rowCol[0] + 0.5, this.rowCol[1] + 0.5]);
			Graphics2D.Draw.triangle
				( p1[0], p1[1]
				, p2[0], p2[1]
				, p3[0], p3[1]
				, 255, 255, 255, 64
				);
			Graphics2D.Draw.line
				( p1[0], p1[1]
				, p3[0], p3[1]
				, this.level.zoom
				, 0, 0, 0
				);
			Graphics2D.Draw.line
				( p2[0], p2[1]
				, p3[0], p3[1]
				, this.level.zoom
				, 0, 0, 0
				);
		}
		// Draw time.
		characterBottomRight =
			[ characterTopLeft[0] + this.level.tileSize
			, characterTopLeft[1] + this.level.tileSize
			];
		this.level.drawNumber(this.time, characterBottomRight);
	}
}
